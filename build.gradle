/*
 * This build file was auto generated by running the Gradle 'init' task
 * by 'simon' at '02.07.16 22:45' with Gradle 2.14
 *
 * This generated file contains a sample Java project to get you started.
 * For more details take a look at the Java Quickstart chapter in the Gradle
 * user guide available at https://docs.gradle.org/2.14/userguide/tutorial_java_projects.html
 */

plugins {
    id "ca.coglinc.javacc" version "2.4.0"
    id "com.jfrog.bintray" version "1.8.4"
}

// Apply the java plugin to add support for Java
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: "jacoco"
apply plugin: 'maven'
apply plugin: 'maven-publish'


version = '0.1.4'
sourceCompatibility = 1.8
targetCompatibility = 1.8

ext {
    javaccOutput = file(project.buildDir.absolutePath + '/generated/javacc/ch/poole/osm/josmfilterparser')
    mainSource = file('src/main/java/ch/poole/osm/josmfilterparser')
    testSource = file('src/test/java/ch/poole/osm/josmfilterparser')
}

compileJavacc {
    inputDirectory = mainSource
    outputDirectory = javaccOutput
}

compileJava {
    source javaccOutput,mainSource
}

compileTestJava {
    source javaccOutput,mainSource,testSource
}

javadoc {
    source mainSource, testSource
    exclude '**/*.jj'
}

task sourcesJar(type: Jar) {
    from mainSource, testSource
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
    //	options.encoding = 'UTF-8'
}

task replaceVersion {
    doLast {
        ant.replaceregexp(match:'ch\\.poole\\:.osm\\:JosmFilterParser\\:([0-9\\.]+)', replace:"ch.poole.osm:JosmFilterParser:${version}", flags:'g', byline:true) {
            fileset(dir: '.', includes: 'README.md')
        }
    }
}
build.dependsOn replaceVersion

// test input may change and require rerunning the tests
test.inputs.files("test-data/filter.txt","test-data/filter.txt-result")

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            groupId 'ch.poole.osm'
            artifactId 'JosmFilterParser'
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['MyPublication']
    pkg {
        repo = 'osm'
        name = 'JosmFilterParser'
        licenses = ['BSD 3-Clause']
        vcsUrl = 'https://github.com/simonpoole/JosmFilterParser.git'
        version {
            name = "${project.version}"
            released  = new Date()
            gpg {
                sign = true //Determines whether to GPG sign the files. The default is false
                passphrase = System.getenv('BINTRAY_PASSPHRASE')
            }
        }
    }
}

// In this section you declare where to find the dependencies of your project
repositories {
    mavenCentral()
    maven { url 'https://dl.bintray.com/simonpoole/osm' }
}

// In this section you declare the dependencies for your production and test code
dependencies {
    compile 'org.jetbrains:annotations:15.0'
    testCompile 'junit:junit:4.12'
}


